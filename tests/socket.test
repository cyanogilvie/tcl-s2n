source [file join [file dirname [info script]] common.tcl]

tcltest::testConstraint have_aio [expr {![catch {package require aio}]}]

test socket-1.1 {Negotiate as a client with www.google.com, blocking basechan} -constraints knownBug -body { #<<<
	set chans_before	[lsort [chan names]]
	set sock	[s2n::socket www.google.com 443]
	chan configure $sock -buffering none -translation crlf -encoding ascii
	puts -nonewline $sock "HEAD / HTTP/1.1\nHost: www.google.com\nConnection: close\nUser-Agent: TLS test\n\n"
	read $sock
} -cleanup {
	if {[info exists sock] && $sock in [chan names]} {close $sock}
	if {[lsort [chan names]] ne $chans_before} {puts stderr "WARNING: $chans_before != [lsort [chan names]]"}
	unset -nocomplain sock
} -match glob -result {HTTP/1.1 200 *}
#>>>
test socket-1.2 {Negotiate as a client with www.google.com, non-blocking basechan} -constraints {have_aio knownBug} -body { #<<<
	set sock	[s2n::socket -async www.google.com 443]
	chan configure $sock -blocking no -buffering none -translation crlf -encoding ascii
	puts -nonewline $sock "HEAD / HTTP/1.1\nHost: www.google.com\nConnection: close\nUser-Agent: TLS test\n\n"
	set rx {}
	while {![eof $sock]} {
		aio waitfor readable $sock
		append rx [read $sock]
	}
	set rx
} -cleanup {
	if {[info exists sock] && $sock in [chan names]} {close $sock}
	unset -nocomplain sock rx
} -match glob -result {HTTP/1.1 200 *}
#>>>
test socket-3.1 {abandon connection, blocking, complete} -setup { #<<<
	set child	[interp create]
	$child eval {load {} S2n}
} -body {
	$child eval {
		set sock	[s2n::socket www.google.com 443]
		chan configure $sock -buffering none -translation crlf -encoding ascii
		puts -nonewline $sock "GET / HTTP/1.1\nHost: www.google.com\nUser-Agent: TLS test\nContent-Length: 0\n\n"
		set status_line	[gets $sock]
		list [expr {$sock in [chan names]}] [string match {HTTP/1.1 200 *} $status_line]
	}
} -cleanup {
	interp delete $child
	unset -nocomplain child status_line
} -result {1 1}
#>>>
test socket-3.2 {abandon connection, nonblocking, in progress} -constraints knownBug -setup { #<<<
	set child	[interp create]
	$child eval {load {} S2n}
} -body {
	$child eval {
		set sock	[s2n::socket -async www.google.com 443]
		chan configure $sock -blocking no -buffering none -translation crlf -encoding ascii
		puts -nonewline $sock "HEAD / HTTP/1.1\nHost: www.google.com\nConnection: close\nUser-Agent: TLS test\n\n"
	}
} -cleanup {
	interp delete $child
	unset -nocomplain child
} -result xx
#>>>

# cleanup
::tcltest::cleanupTests
return

# vim: ft=tcl foldmethod=marker foldmarker=<<<,>>> ts=4 shiftwidth=4
